<?xml version="1.0" encoding="utf-8"?>

<mx:Box backgroundColor="white" borderStyle="solid" direction="vertical"  
	xmlns:mx="http://www.adobe.com/2006/mxml"  verticalGap="0"
	implements="flash.utils.IExternalizable" creationComplete="init()" fontSize="9" 
	verticalScrollPolicy="off" horizontalScrollPolicy="off">
<mx:Script>
	<![CDATA[
		import Util.HashMap;
		import mx.collections.ICollectionView;
		import mx.controls.ComboBox;
		import mx.controls.HScrollBar;
		import mx.containers.HBox;
		import mx.rpc.events.ResultEvent;
		import DAG;
		import mx.events.MenuEvent;
		import mx.controls.Alert;
		import mx.skins.halo.BrokenImageBorderSkin;
		import mx.collections.ArrayCollection;
		import mx.rpc.events.FaultEvent;
		import flash.utils.IDataInput;
		import flash.utils.IDataOutput;
		//import mx.controls.Alert;
		import mx.core.Repeater;
		import mx.controls.Menu;
		import mx.collections.Sort;
		import mx.collections.SortField;
		import Components.DAGPath;
			
		private var editedNode:DAGNode;	
		[Bindable]
		public var accSelectedIndx:Number = 0;
		[Bindable]
		public var nodeName:String = "Node";
		[Bindable]
		public var expressionId:int;
		[Bindable]
		public var nodeNumber:String = "1";
		[Bindable]
		public var operatorBetweenAttrAndAssociation:String="AND"; 
		[Bindable]
		public var nodeType:String="";
		[Bindable]
		public var operatorArray:Array = ["AND","OR"];
		[Bindable]
		public var operatorSelectedIndex:int=0;
		[Bindable]
		public var enable:Boolean=true;
		[Bindable]
		public var nodeColor:int =0xFFFFAA;
		public var errorMsg:String= "";
		public var attachedNodeMap:HashMap = new HashMap();
		public var associationList:ArrayCollection;
		public var operatorList:ArrayCollection;

		public var dagpathList:ArrayCollection;
		public var outAssociations:ArrayCollection = new ArrayCollection();		
		[Bindable]
		public var displayoutAssociations:ArrayCollection = new ArrayCollection();		
		public var inAssociations:ArrayCollection = new ArrayCollection();
		public var orderOfAss:HashMap=new HashMap();
		public static var orderAssociation:int=0;
		
		// The Array data provider
		[Bindable]
		public var menuData:Array = [	
		{label: "Edit", type: "radio", toggled: false,enabled:true},
		{label: "Delete", type: "radio", toggled: false,enabled:true}
		
		];
		
		[Bindable]
		public var outputMenuData:Array = [	
		{label: "Add To View", type: "radio", toggled: false,enabled:false},
		{label: "Delete From View", type: "radio", toggled: false,enable:true}
		
		];

		/**
		* Add out association to be displayed. 
		*/
		[Bindable]
		public function addDisplayOutAssociation(node:String,link:String,operatorIndex:int):void{
			var order:int=0;
			if(orderOfAss!=null)
			{
				if(orderOfAss.containsKey(link))
				{
					order=orderOfAss.getValue(link);
				}
				else
				{
					order=orderAssociation++;
					orderOfAss.put(link,order);
				}
			}
			var ass:Association = new Association(node,link,operatorIndex,order);
			displayoutAssociations.addItem(ass);
			
			 var col:ICollectionView = displayoutAssociations  as ICollectionView;
			 var sort:Sort = new Sort();
		     sort.fields = [new SortField("order",true)];
		     col.sort = sort;
			 col.refresh();
	
		}
		/**
		* Remove displayed out association
		*/
		[Bindable]
		public function removeDisplayOutAssociation(node:String,link:String):void{
			var ass:Association;
			for(var i:int=0;i<displayoutAssociations.length;i++)
			{
				ass = Association(displayoutAssociations.getItemAt(i));
				if(ass.associatedLink == link && ass.associatedNode == node)
				{
					displayoutAssociations.removeItemAt(i);
					break;
				}
			}
		}

		/**
		* Add out association to be displayed. 
		*/
		[Bindable]
		public function addOutAssociation(node:String,link:String,operatorIndex:int):void{
			var order:int=0;
			if(orderOfAss!=null)
			{
				if(orderOfAss.containsKey(link))
				{
					order=orderOfAss.getValue(link);
				}
				else
				{
					order=orderAssociation++;
					orderOfAss.put(link,order);
				}
			}
			var ass:Association = new Association(node,link,operatorIndex,order);
			outAssociations.addItem(ass);
			 var col:ICollectionView = outAssociations  as ICollectionView;
			 var sort:Sort = new Sort();
		     sort.fields = [new SortField("order",true)];
		     col.sort = sort;
			 col.refresh();
		}
		/**
		* Remove out association
		*/	
		[Bindable]
		public function removeOutAssociation(node:String,link:String):void
		{
			var ass:Association;
			for(var i:int=0;i<outAssociations.length;i++)
			{
				ass = Association(outAssociations.getItemAt(i));
				if(ass.associatedLink == link && ass.associatedNode == node)
				{
					outAssociations.removeItemAt(i);
					break;
				}
			}
		}
		
		[Bindable]
		public function getOutAssociations():ArrayCollection{
			return outAssociations;
		}
			
		[Bindable]
		public function getOutAssociation(node:String):Association{
			var ass:Association;
			for(var i:int=0;i<outAssociations.length;i++)
			{
				ass = Association(outAssociations.getItemAt(i));
				if(ass.associatedNode == node)
				{
					return ass;
				}
			}
			return null;
		}
		/**
		* Add in association to be displayed. 
		*/
		[Bindable]
		public function addInAssociation(node:String,link:String,operatorIndex:int):void{
			var order:int=0;
			var ass:Association = new Association(node,link,operatorIndex,order);
			inAssociations.addItem(ass);
		}
		/**
		* Remove in association
		*/	
		[Bindable]
		public function removeInAssociation(node:String,link:String):void{
			var ass:Association;
			for(var i:int=0;i<inAssociations.length;i++)
			{
				ass = Association(inAssociations.getItemAt(i));
				if(ass.associatedLink == link && ass.associatedNode == node)
				{
					inAssociations.removeItemAt(i);
					break;
				}
			}
		}
		[Bindable]
		public function getInAssociations():ArrayCollection{
			return inAssociations;
		}
		
		/**
		*Init method
		*/
		public function init():void
		{ 
			this.parentApplication.rpcService.addEventListener("fault", faultHandler);
		}
		/**
		*Initilize DAG node before creation
		*/
		public function initializeNode(nodeName:String,nodeType:String,expressionId:int,toolTipStr:String,x:int,y:int,nodeColor:int):void
		{
			this.x =x;
			this.y =y;
			this.nodeName = nodeName;
			this.nodeType =	nodeType;
			this.nodeNumber = expressionId+"";
			this.expressionId=expressionId;
			this.name = expressionId+"";
			this.toolTip = toolTipStr;
			this.nodeColor=nodeColor;
		}
		/**
		* Get node width
		*/
		public function getWidth():int{
	      return this.width;
	    }
   		/**
		* Get node height
		*/
	    public function getHeight():int{
	      return this.height;
	    }
   		/**
		* Get node x co-ordinate
		*/
	    public function getX():int{
	      return this.x;
	    }
	    /**
		* Get node y co-ordinate
		*/
	    public function getY():int{
	      return this.y;
	    }
		/**
		* Read serialized data
		*/
		public function readExternal(input:IDataInput):void {
			nodeName = input.readUTF();
			toolTip=input.readUTF();
			expressionId=input.readInt();
			operatorBetweenAttrAndAssociation = input.readUTF();
			nodeType = input.readUTF();
			associationList = input.readObject() as ArrayCollection;
			operatorList = input.readObject() as ArrayCollection;
			dagpathList = input.readObject() as ArrayCollection;
			errorMsg = input.readUTF();
			x = input.readInt();
			y = input.readInt();
		}
		/**
		* Write serialized data.
		*/
		public function writeExternal(out:IDataOutput):void {
			out.writeUTF(nodeName);
			out.writeUTF(toolTip);
			out.writeInt(expressionId);
			out.writeUTF(operatorBetweenAttrAndAssociation);
			out.writeUTF(nodeType);
			out.writeObject(associationList);
			out.writeObject(operatorList);
			out.writeObject(dagpathList);
			out.writeUTF(errorMsg);
			out.writeInt(x);
			out.writeInt(y);
		}
		//---
		/**
		* Select node
		*/
		public function select():void
		{
			this.setStyle("borderColor","blue");
			this.setStyle("borderThickness","2");
		}
		/**
		* Unselect node
		*/
		public function unSelect():void
		{
			this.setStyle("borderColor","gray");
			this.setStyle("borderThickness","1");
		}
			
		/**
		* Method to create an Array-based menu.
		*/
		private function createAndShow(event:MouseEvent):void {
			var myMenu:Menu=null;
			if(this.parentApplication.view=="Result")
			{
				myMenu= Menu.createMenu(this, outputMenuData, true);
				if(this.nodeType==DAGConstants.CONSTRAINT_ONLY_NODE)
				{
					this.outputMenuData[0].enabled=true;
					this.outputMenuData[1].enabled=false;
				}
			}
			else
			{
				myMenu = Menu.createMenu(this, menuData, true);
			}
			myMenu.show(event.stageX,event.stageY);
			myMenu.addEventListener(MenuEvent.ITEM_CLICK,handleMenuEvent);
			
		}
		/**
		* Delete path associated with node
		*/
		private function deletePath(pathName:String):void
		{
			var dagPath:DAGPath=this.parentApplication.dagPathUIMap.getValue(pathName) as DAGPath;
			var node1:String=dagPath.sourceExpId+"";
			var node2:String=dagPath.destinationExpId+"";
			var dagNode1:DAGNode = DAGNode(this.parent.getChildByName(node1));
			var dagNode2:DAGNode = DAGNode(this.parent.getChildByName(node2));
			dagPath.deletePath(dagNode1,dagNode2);
			orderOfAss.remove(pathName);
		}
		/**
		* Delete node.
		*/
		public function deleteNode():void
		{
				
				var ass:Association;
				var length:int  = inAssociations.length as int;
				for(var i:int=0;i<length;i++)
				{
					ass = Association(inAssociations.getItemAt(0));
					deletePath(ass.associatedLink);
					DAGNode(this.parent.getChildByName(ass.associatedNode)).removeOutAssociation(this.name,ass.associatedLink);
					DAGNode(this.parent.getChildByName(ass.associatedNode)).removeDisplayOutAssociation(this.name,ass.associatedLink);
				}
				length=outAssociations.length as int;
				for(var j:int=0;j<length;j++)
				{
					ass = Association(outAssociations.getItemAt(0));
					deletePath(ass.associatedLink);
					DAGNode(this.parent.getChildByName(ass.associatedNode)).removeInAssociation(this.name,ass.associatedLink);
				}
				this.parentApplication.rpcService.deleteNode(this.expressionId);
				this.parentApplication.rpcService.addEventListener(ResultEvent.RESULT,deleteHandler);
				
		}
		/**
		* Handling menu click event 
		*/
		private function handleMenuEvent(event:MenuEvent):void
		{
			if(event.label == DAGConstants.DELETE)
			{
				deleteNode();
			}
			if(event.label ==DAGConstants.EDIT)
			{
				this.parentApplication.editNode(this);
			}
			if(event.label==DAGConstants.ADD_TO_VIEW)
			{
				this.nodeColor=0xFFFFAA;
				this.outputMenuData[0].enabled=false;
				this.outputMenuData[1].enabled=true;
				this.parentApplication.rpcService.addToView(this.expressionId);
				this.parentApplication.rpcService.addEventListener(ResultEvent.RESULT,addToViewHandler);
			}
			if(event.label==DAGConstants.DELETE_FROM_VIEW)
			{
				if(this.nodeType==DAGConstants.VIEW_ONLY_NODE)
				{
					deleteNode();
				}				
				else
				{
					this.nodeColor=0xffb693;
					this.outputMenuData[0].enabled=true;
					this.outputMenuData[1].enabled=false;
					this.parentApplication.rpcService.deleteFromView(this.expressionId);
					this.parentApplication.rpcService.addEventListener(ResultEvent.RESULT,deleteFromViewHandler);
					
				}
				
			}
		}
		/**
		* Handler for delete from result view.
		*/
		public function deleteFromViewHandler(event:ResultEvent):void
		{
			this.parentApplication.rpcService.removeEventListener(ResultEvent.RESULT,deleteFromViewHandler,false);
		}
		/**
		* Handler for add to result view 
		*/
		public function addToViewHandler(event:ResultEvent):void
		{
			this.parentApplication.rpcService.removeEventListener(ResultEvent.RESULT,addToViewHandler,false);
		}
		/**
		* Delete node handler 
		*/
		public function deleteHandler(event:ResultEvent):void
		{
			for(var j:int =0;j<this.parentApplication.selectedNodeList.length;j++)	
			{
				var dd:DAGNode = this.parentApplication.selectedNodeList.getItemAt(j) as DAGNode;
				dd.unSelect();
			}
			this.parent.removeChild(this.parent.getChildByName(this.name));
			for(var i:int =0;i<this.parentApplication.totalNodes.length;i++)	
			{
				var dagNode:DAGNode = this.parentApplication.totalNodes.getItemAt(i) as DAGNode;
				if(dagNode.name==this.name)
				{
					this.parentApplication.totalNodes.removeItemAt(i);
					this.parentApplication.selectedNodeList.removeAll();
					this.parentApplication.count = 0;
					this.parentApplication.node1 = "";
					this.parentApplication.node2 = "";
				}
			
			}
			this.parentApplication.rpcService.removeEventListener(ResultEvent.RESULT,deleteHandler,false);
			if(this.nodeType==DAGConstants.CONSTRAINT_VIEW_NODE)
			{
				var paramArray:Array = new Array(DAGConstants.DELETE_LIMITS_MESSAGE);
				ExternalInterface.call("showEntityInformation",paramArray);
			}
			this.parentApplication.currentExp.text = this.parentApplication.getExprssionString();
		}
		/**
		* Error handler
		*/
		public function faultHandler (event:FaultEvent):void {
				// Deal with event.fault.faultString, etc.
			Alert.show(event.fault.faultString, 'Error');
		}
		/**
		* Change operator handler
		*/	
		public function setOperatorHandler(event:ResultEvent):void
		{
			this.parentApplication.rpcService.removeEventListener(ResultEvent.RESULT,setOperatorHandler,false);
		}
		/**
		* Changing logical operator
		*/
		public function operatorChange(event:Event):void
		{
		 var cb:ComboBox=event.currentTarget as ComboBox;
	     var index:int = parseInt(cb.name) as int;

		 this.parentApplication.rpcService.addEventListener(ResultEvent.RESULT,setOperatorHandler);
		 this.parentApplication.rpcService.setLogicalOperator(this,index,cb.selectedLabel);
		 
		 var outAssociation:Association =  displayoutAssociations.getItemAt(index) as Association;
		 outAssociation.operatorIndex=cb.selectedIndex;

		 var dagPath:DAGPath  =this.parentApplication.dagPathUIMap.getValue(outAssociation.associatedLink) as DAGPath;
		 dagPath.operatorIndex = cb.selectedIndex;
		 this.parentApplication.currentExp.text = this.parentApplication.getExprssionString();	 	 		
		}
	
		
	]]>
</mx:Script>
	<mx:HBox name="Node" backgroundColor="{nodeColor}" borderStyle="solid" width="100%" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">
		<mx:Canvas  backgroundColor="0xF5F5F5"  borderStyle="solid">
			<mx:Label id="nodeNo" text="{nodeNumber}" mouseChildren="false"/>
		</mx:Canvas>
		<mx:Label  styleName="Arial" text="{nodeName}" color="blue" width="100" mouseChildren="false"/>
		
		<mx:Button id="createMenu" label=">" click="createAndShow(event);" mouseChildren="false"/>
	</mx:HBox>
		<mx:VBox id="assVbox" name="Association" label="Associations" width="100%" verticalGap="0" visible="false">
		
			<mx:Repeater id="assRep" name="Repeater" dataProvider="{displayoutAssociations}">
			
				<mx:HBox name="hbox" width="100%" borderStyle="solid">
				<mx:Canvas backgroundColor="0xF5F5F5"  borderStyle="solid">
					<mx:Label text="{assRep.currentItem.associatedNode}" mouseChildren="false" />
				</mx:Canvas>
				<mx:ComboBox id="operatorCombo"  name="{assRep.currentIndex.toString()}"  dataProvider="{operatorArray}" 
					change="operatorChange(event)"  selectedIndex="{assRep.currentItem.operatorIndex}" enabled="{enable}"/>
		
			</mx:HBox>
			</mx:Repeater>
		</mx:VBox>
</mx:Box>